plugins {
    id "org.jlleitschuh.gradle.ktlint" version "12.1.0"
    id 'org.jetbrains.kotlin.jvm' version '1.9.10'
}

repositories {
    mavenCentral()
}

def projectVersion = "1.0.0"
group = "com.IngsisMEGE"

subprojects {
    version = projectVersion

    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'org.jlleitschuh.gradle.ktlint'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'maven-publish'


    dependencies {
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        dependsOn test

        reports {
            xml.required = false
            csv.required = false
            html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        }
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/IngsisMEGE/printScript-2024")
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                from(components.java)
            }
        }
    }
}

task installGitHooks(type: Copy) {
    from new File(rootProject.rootDir, 'pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks') }
}

task gitExecutableHooks {
    doLast {
        Runtime.getRuntime().exec("chmod -R +x .git/hooks/");
    }
}

installGitHooks.dependsOn gitExecutableHooks
clean.dependsOn installGitHooks